\ ttyline.ok - terminal user line input
\ Copyright (C) 2018 Wolfgang Jaehrling
\
\ ISC License
\
\ Permission to use, copy, modify, and/or distribute this software for any
\ purpose with or without fee is hereby granted, provided that the above
\ copyright notice and this permission notice appear in all copies.
\
\ THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
\ WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
\ MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
\ ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
\ WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
\ ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
\ OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

create: line 120 allot
var:    pos
: 0line [0 line c!];
: 0pos  [line pos !];
: +pos  [pos 1+!];
: -pos  [pos 1-!];
: bol?  [pos @ line =];
: eol?  [pos @ c@ 0=];
: -bol^ [bol?] if [rdrop] then ;
: -eol^ [eol?] if [rdrop] then ;

: press-right [-eol^   +pos 1 right flush];
: press-left  [-bol^   -pos 1 left  flush];
: right^^     [over key:right =] if [2drop press-right 2rdrop] then ;
: left^^      [over key:left  =] if [2drop press-left  2rdrop] then ;

: end        [pos @] begin [dup c@ 0<>] while [1+] repeat ;
: push       [end pos @ 1-] for [dup c@ over 1+ c! 1-] next ;
: redraw>eol [pos @ end] for [dup c@ emit 1+] next [end pos @ - 0>?] if [left] then ;
: cinsert    [push   dup emit   pos @ c! +pos   redraw>eol   flush];

: alt^        [mod:alt  over and] if [2drop rdrop] then ;
: ctrl^       [mod:ctrl over and] if [2drop rdrop] then ;
: cursor^     [right^^ left^^];
: graphic^    [mod:none =?] if [drop cinsert rdrop] then ;
: handle-char [alt^ ctrl^ cursor^ graphic^ 2drop];
: input       begin [key+mod over key:enter <>] while [handle-char] repeat ;

: readline [0line 0pos +raw input -raw line];

\ test
: rl [key drop readline];
rl
